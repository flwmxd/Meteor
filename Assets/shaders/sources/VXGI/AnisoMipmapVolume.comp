#version 450
layout (local_size_x = 8, local_size_y = 8, local_size_z = 8) in;

#include "VXGI.glsl"

layout(binding = 0, rgba8) uniform writeonly image3D uVoxelMipmapOut[6];
//0 1 2 3 4 5
layout(binding = 6) 	   uniform sampler3D uVoxelMipmapIn[6];
//6 7 8 9 10 11
layout(binding = 12) 	   uniform UniformBufferObject
{
	vec3 mipDimension;
	int mipLevel;
}ubo;

void fetchTexels(ivec3 pos, int dir, inout vec4 val[8]) 
{
	for(int i = 0; i < 8; i++)
	{
		val[i] = texelFetch(uVoxelMipmapIn[dir], pos + anisoOffsets[i], ubo.mipLevel);
	}
}

void main()
{
	if(gl_GlobalInvocationID.x >= ubo.mipDimension.x ||
		gl_GlobalInvocationID.y >= ubo.mipDimension.y ||
		gl_GlobalInvocationID.z >= ubo.mipDimension.z) return;

	ivec3 writePos = ivec3(gl_GlobalInvocationID);
	ivec3 sourcePos = writePos * 2;
	// fetch values
	vec4 values[8];
	// x -
	fetchTexels(sourcePos, 0, values);
	imageStore(uVoxelMipmapOut[0], writePos, 
	(
		values[0] + values[4] * (1 - values[0].a) + 
		values[1] + values[5] * (1 - values[1].a) +
		values[2] + values[6] * (1 - values[2].a) +
		values[3] + values[7] * (1 - values[3].a)) * 0.25f
	);
	// x +
	fetchTexels(sourcePos, 1, values);
    imageStore(uVoxelMipmapOut[1], writePos, 
	(
		values[4] + values[0] * (1 - values[4].a) +
    	values[5] + values[1] * (1 - values[5].a) +
    	values[6] + values[2] * (1 - values[6].a) +
    	values[7] + values[3] * (1 - values[7].a)) * 0.25f
    );
	// y -	
	fetchTexels(sourcePos, 2, values);
    imageStore(uVoxelMipmapOut[2], writePos, 
	(
		values[0] + values[2] * (1 - values[0].a) +
    	values[1] + values[3] * (1 - values[1].a) +
    	values[5] + values[7] * (1 - values[5].a) +
    	values[4] + values[6] * (1 - values[4].a)) * 0.25f
    );
	// y +
	fetchTexels(sourcePos, 3, values);
    imageStore(uVoxelMipmapOut[3], writePos, 
	(
		values[2] + values[0] * (1 - values[2].a) +
    	values[3] + values[1] * (1 - values[3].a) +
    	values[7] + values[5] * (1 - values[7].a) +
    	values[6] + values[4] * (1 - values[6].a)) * 0.25f
    );
	// z -
	fetchTexels(sourcePos, 4, values);
    imageStore(uVoxelMipmapOut[4], writePos, 
	(
		values[0] + values[1] * (1 - values[0].a) +
    	values[2] + values[3] * (1 - values[2].a) +
    	values[4] + values[5] * (1 - values[4].a) +
    	values[6] + values[7] * (1 - values[6].a)) * 0.25f
    );
	// z +
	fetchTexels(sourcePos, 5, values);
    imageStore(uVoxelMipmapOut[5], writePos, 
	(
		values[1] + values[0] * (1 - values[1].a) +
    	values[3] + values[2] * (1 - values[3].a) +
    	values[5] + values[4] * (1 - values[5].a) +
    	values[7] + values[6] * (1 - values[7].a)) * 0.25f
    );
}